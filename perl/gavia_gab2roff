#!/usr/bin/perl -w # -*- perl -*-
#
# $Id: gavia_gab2roff,v 1.3 2002-12-07 19:01:41 grahn Exp $
# $Name:  $
#
# gavia_gab2text - transform gab (see gavia(5)) formatted excursions
# to roff (groff, nroff, troff) source
#
# Copyright (c) 2002 Jörgen Grahn <jgrahn@algonet.se>
# All rights reserved.
#

use strict;

my $commentw = 60;
my $have_latin1 = 1;

print <<EOF;
.\" generated by $0
.\"
EOF

while(<>) {

    chomp;
    s/^\s+//;

    SWITCH: {

	my $hdrs = "place|date|time|observers|weather|comments";
      
	(/^$/ || /^\s*\#/) && do {

	    last SWITCH;
	};

	/^(date)\s*:\s*(.+)/i && do {

	    # special treatment for the date field
	    my $head = $1;
	    my $date = $2;
	    $date =~ s/(\d{4})(\d{2})(\d{2})/$1--$2--$3/
		or
		die "gavia_gab2roff: broken date at line $.: $2\n";
	    $_ = "$head: $date";
	};

	/^($hdrs)\s*:\s*(.+)/i && do {

	    printf("%s\tT{\n%s\nT}\n", $1, quote($2));
	    last SWITCH;
	};

	/^{/ && do {

	    print <<EOF;
.TS
| ri s | lw$commentw |.
_
EOF
	    last SWITCH;
	};

	/^}{/ && do {

	    print <<EOF;
_
.T&
| ri | r | l |.
EOF
	    last SWITCH;
	};

	/^}/ && do {

	    print <<EOF;
_
.TE
.sp
EOF
	    last SWITCH;
	};

	/^(.+?)\s*:.*?:\s*(\d+)\s*:\s*(.*)/ && do {

	    printf("%s\t%s\tT{\n%s\nT}\n",
		   quote($1), $2, quote($3));
	    last SWITCH;
	};

	/^(.+?)\s*:.*?:.*?:\s*(.*)/ && do {

	    printf("%s\t\tT{\n%s\nT}\n",
		   quote($1), quote($2));
	    last SWITCH;
	};

	print STDERR "gavia_gab2roff: funny stuff on line $.: $_\n";
    }
}

exit 0;

sub quote {
    my $s = shift;

    $s =~ s|\\|\\(bs|g;
    $s =~ s|\t| |g;

    $s =~ s|\"|''|g;
    $s =~ s|---|\\(em|g;
    $s =~ s|--|\\(en|g;
    $s =~ s|×|\\(mu|g;

    return $s if $have_latin1;

    $s =~ s|é|\\('e|g;
    $s =~ s|ä|\\(:a|g;
    $s =~ s|Ä|\\(:A|g;
    $s =~ s|ö|\\(:o|g;
    $s =~ s|Ö|\\(:O|g;
    $s =~ s|å|\\(oa|g;
    $s =~ s|Å|\\(oA|g;

    return $s;
}
