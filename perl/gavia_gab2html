#!/usr/bin/perl -w # -*- perl -*-
#
# $Id: gavia_gab2html,v 1.5 2006-01-02 22:15:25 grahn Exp $
# $Name:  $
#
# gavia_gab2html - transform gab (see gavia(5)) formatted excursions
# to HTML.
#
# Copyright (c) 2002 Jörgen Grahn
# All rights reserved.
#

use strict;
use POSIX;

my $charset = 'iso-8859-1';

print <<EOF;
<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01//EN" "http://www.w3.org/TR/html4/strict.dtd">
<html>
<head>
<meta http-equiv="Content-Type" content="text/html; charset=$charset">
<title>Gavia contents</title>
</head>
<body>
EOF


while(<>) {

    chomp;
    s/^\s+//;

    SWITCH: {

	my $hdrs = "place|date|time|observers|weather|comments";
      
	(/^$/ || /^\s*\#/) && do {

	    last SWITCH;
	};

	/^(date)\s*:\s*(.+)/i && do {

	    # special treatment fr the date field
	    my $head = $1;
	    my ($date, $wday) = mkdate($2);
	    printf("<dt>%s <dd>%s (%s)\n", $head, quote($date), $wday);
	    last SWITCH;
	};

	/^($hdrs)\s*:\s*(.+)/i && do {

	    printf("<dt>%s <dd>%s\n", $1, quote($2));
	    last SWITCH;
	};

	/^{/ && do {

	    print "\n<hr><p><dl>\n";
	    last SWITCH;
	};

	/^}{/ && do {

	    # BUG if there are no species -- <dl></dl> is forbidden
	    print "</dl><p><dl>\n";
	    last SWITCH;
	};

	/^}/ && do {

	    print "</dl><p>\n";
	    last SWITCH;
	};

	/^(.+?)\s*:.*?:\s*(\d+)\s*:\s*(.*)/ && do {

	    printf("<dt><em>%s</em>\n<dd>(%s) %s\n", $1, $2, quote($3));
	    last SWITCH;
	};

	/^(.+?)\s*:.*?:.*?:\s*(.*)/ && do {

	    printf("<dt><em>%s</em>\n<dd>%s\n", $1, quote($2));
	    last SWITCH;
	};

	print STDERR "gavia_gab2html: funny stuff on line $.: $_\n";
    }
}


print <<EOF;
<hr>
</body>
</html>
EOF

exit 0;


sub quote {
    my $s = shift;

    $s =~ s|&|&amp;|g;
    $s =~ s|<|&lt;|g;
    $s =~ s|>|&gt;|g;

    $s =~ s|---|&mdash;|g;
    $s =~ s|--|&ndash;|g;

    return $s;
}


# Given a date on the format 'yyyymmdd',
# return it converted to ISO format and
# also return its day-of-week.
#
sub mkdate {
    my $date = shift;

    $date =~ s/(\d{4})(\d{2})(\d{2})/$1-$2-$3/
	or
	    die "gavia_gab2html: broken date at line $.: $2\n";

    my @t = gmtime(POSIX::mktime(0, 0, 12, $3, $2-1, $1-1900));
    my @days = ("Sun", "Mon", "Tue", "Wed", "Thu", "Fri", "Sat");
    my $wday = $days[$t[6]];

    return ($date, $wday);
}
