#!/usr/bin/perl -w # -*- perl -*-
#
# $Id: gavia_gab2text,v 1.3 2002-12-28 22:23:28 grahn Exp $
# $Name:  $
#
# gavia_gab2text - transform gab (see gavia(5)) formatted excursions
# to plain text
#
# Copyright (c) 2002 Jörgen Grahn <jgrahn@algonet.se>
# All rights reserved.
#

use strict;
use POSIX;

while(<>) {

    chomp;
    s/^\s+//;

    SWITCH: {

	my $hdrs = "place|date|time|observers|weather|comments";
      
	(/^$/ || /^\s*\#/ || /^}\s*$/) && do {

	    last SWITCH;
	};

	/^(date)\s*:\s*(.+)/i && do {

	    # special treatment fr the date field
	    my $head = $1;
	    my ($date, $wday) = mkdate($2);
	    bprint(sprintf("%-9s: %s (%s)\n", $head, $date, $wday));
	    last SWITCH;
	};

	/^($hdrs)\s*:\s*(.+)/i && do {

	    bprint(sprintf("%-9s: %s\n", $1, $2));
	    last SWITCH;
	};

	/^({|}{)/ && do {

	    print "------------------------------\n";
	    last SWITCH;
	};

	/^(.+?)\s*:.*?:\s*(\d+)\s*:\s*(.*)/ && do {

	    bprint(sprintf("%s (%d) %s\n", $1, $2, $3));
	    last SWITCH;
	};

	/^(.+?)\s*:.*?:.*?:\s*(.*)/ && do {

	    bprint(sprintf("%s %s\n", $1, $2));
	    last SWITCH;
	};

	print STDERR "gavia_gab2text: funny stuff on line $.: $_\n";
    }
}

exit 0;

# Print a long, \n-terminated string,
# while breaking and TAB-indenting it at
# word boundaries.
#
sub bprint {
    my $s = shift;

    while($s =~ s/^(.{60,80})\s+(.{8,})/$2/) {
	print "$1\n\t";
    }
    print $s;
}

# Given a date on the format 'yyyymmdd',
# return it converted to ISO format and
# also return its day-of-week.
#
sub mkdate {
    my $date = shift;

    $date =~ s/(\d{4})(\d{2})(\d{2})/$1-$2-$3/
	or
	    die "gavia_gab2text: broken date at line $.: $2\n";

    my @t = gmtime(POSIX::mktime(0, 0, 12, $3, $2-1, $1-1900));
    my @days = ("Sun", "Mon", "Tue", "Wed", "Thu", "Fri", "Sat");
    my $wday = $days[$t[6]];

    return ($date, $wday);
}
