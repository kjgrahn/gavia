#!/usr/bin/perl -w
#
# $Id: gavia_unfold,v 1.3 2007-06-22 20:39:07 grahn Exp $
# $Name:  $
#
# gavia_unfold.pl - unfold continuation lines in 'gab' files
#                   (or any texts, really).
#
# Copyright (c) 2006, 2007 Jörgen Grahn.
# All rights reserved.
use strict;

# The state machine defines these events:
# SP  - a blankspace line
# SPX - a non-blank line starting with whitespace
# X - a line starting with non-whitespace
# combined with
# EOF - end of file after this

my @acc = ();

while(<>)
{
    my @aline = ();
    my $bline = '';

    if(/^\s+\S/) { # SPX
	s/^\s+//; s/\s+$//;
	push @acc, $_;
	next unless eof();
	@aline = @acc; @acc = ();
    }
    elsif(/^\s+/) { # SP
	@aline = @acc; @acc = ();
	$bline = $_;
    }
    else { # X
	@aline = @acc;
	if(eof()) {
	    $bline = $_;
	}
	else {
	    s/\s+$//;
	    @acc = ($_);
	}
    }

    my @llines = ();
    push @llines, join(' ', @aline) . "\n" if @aline;
    push @llines, $bline if $bline;

    # Finally, what takes place in this loop is similar to what would
    # have taken place in the while(<>) ... loop, if the unfolding had
    # already taken place in the program's input. Except here folding
    # happens across the file boundaries, but I can live with that.
    foreach (@llines) {
	print;
    }
}

exit 0;
