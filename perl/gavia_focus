#!/usr/bin/perl -w # -*- perl -*-
#
# $Id: gavia_focus,v 1.3 2001-09-30 11:16:47 grahn Exp $
# $Name:  $
#
# gaviafocus - filter out certain species
# from a 'gab' file
#
# Copyright (c) 2001 Jörgen Grahn <jgrahn@algonet.se>
# All rights reserved.
# 
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions
# are met:
# 1. Redistributions of source code must retain the above copyright
#    notice, this list of conditions and the following disclaimer.
# 2. Redistributions in binary form must reproduce the above copyright
#    notice, this list of conditions and the following disclaimer in the
#    documentation and/or other materials provided with the distribution.
# 3. The name of the author may not be used to endorse or promote products
#    derived from this software without specific prior written permission.
# 
# THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' ...etc
#

use strict;
use Getopt::Std;
use vars qw($opt_v $opt_f);

getopt("f");

my $invert = defined($opt_v);

if(defined($opt_f)) {
    open BLACKLIST, $opt_f
	or die "cannot open `$opt_f' for reading\n";
}
else {
    open BLACKLIST, glob("~/.gavia_focus")
	or
    open BLACKLIST, glob("~gavia/gavia_focus")
	or die "cannot open focus filter for reading; exiting\n";
}

my %blacklist;

while(<BLACKLIST>) {
    my $name;
    my $range;

    next if /^\#/;
    next if /^\s*$/;

    if(/\d/) {
	($name, $range) = /^\s*(.+?)\s*(\d+\-\d+)\s*$/;
	die "parse error in filter (line $.); exiting\n"
	    unless defined($name) && defined($range);

	$blacklist{$name}=$range;
    }
    else {
	($name) = /^\s*(.+?)\s*$/;

	$blacklist{$name}="0101-1231";
    }
}

my $date;

while(<>)
{
    my ($start, $end);

    if(/^date\s*:\s*(\d+)/i) {
	$date = $1 % 10000;
    }

    print, next
	unless /^[a-zåäö \t]+:.+?:[0-9 \t]+?:/i;

    my ($name) = /\s*([a-zåäö ]+?)\s*:/i
	or die;

    if(not defined($blacklist{$name})) {
	print unless $invert;
	next;
    }

    # print it anyways, iff out of blacklist range
    ($start, $end) = $blacklist{$name} =~ /(\d+)\-(\d+)/;

    if($start < $end) {
	print if $date<$start || $date>$end xor $invert;
    }
    else {
	print if $date<$start && $date>$end xor $invert;
    }
}

exit 0;
